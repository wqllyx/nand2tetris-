// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/2/ALU.hdl
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
    // 检查x
    Mux16(a=x, b=false, sel=zx, out=zxout); // if (sel = 0) out = a, else out = b
    Not16(in=zxout,out= notzxout);
    Mux16(a=zxout, b=notzxout, sel=nx, out=x1);

    // 检查y
    Mux16(a=y, b=false, sel=zy, out= zyout);
    Not16(in=zyout,out=notzyout);
    Mux16(a=zyout, b=notzyout, sel=ny, out=y1);

    // 检查运算
    And16(a=x1, b=y1, out=xandy);
    Add16(a=x1, b=y1, out=xaddy);
    Mux16(a=xandy, b=xaddy, sel=f, out=out1);

    Not16(in=out1, out=notout1);
    Mux16(a=out1, b=notout1, sel=no, out=out, out[0..7] = lsb8out, out[8..15] = msb8out, out[15] = ng);

    // 检查输出
    Or8Way(in=lsb8out,out=tmp1);
    Or8Way(in=msb8out,out=tmp2);
    Or(a=tmp1,b=tmp2,out=tmp3);
    Not(in=tmp3,out=zr);
    //// Replace this comment with your code.
}